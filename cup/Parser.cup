
import java.util.*;
import java.io.*;
import java_cup.runtime.Symbol;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java_cup.runtime.ComplexSymbolFactory;
import nodes.declarations.*;
import nodes.statements.*;
import nodes.expressions.*;
import nodes.*;
import types.*;

parser code {:
  public boolean syntaxErrors;

  Lexer lexer;

  public Parser(Lexer lex, ComplexSymbolFactory sf) {
    super(lex,sf);
    lexer = lex;
  }
:};

terminal DO, IF, ELSE, VOID, WHILE, OPENPAREN, CLOSEPAREN, OPENBRACE, CLOSEBRACE, OPENBRACKET, CLOSEBRACKET, EQUALS, SEMICOLON, AMPERSAND, COMMA, CONTINUE, BREAK, FOR;

terminal OperatorKind AMPERSANDAMPERSAND, BARBAR, PLUS, MINUS, LESSTHAN, LESSTHANEQUALS, GREATERTHAN, GREATERTHANEQUALS, EQUALSEQUALS, EXCLAMATIONEQUALS, EXCLAMATION, ASTERISK, SLASH, PERCENT;
terminal TypeKind BOOL, INT;
terminal String IDENTIFIER;
terminal Integer INTLITERAL;
terminal Boolean BOOLLITERAL;

non terminal Program program;
non terminal List<Declaration> declarationList;
non terminal Declaration declaration;
non terminal VariableDeclaration variableDeclaration;
non terminal FunctionDeclaration functionDeclaration;
non terminal List<Variable> variableList;
non terminal Variable variable;
non terminal TypeKind type;
non terminal List<Parameter> parameterList;
non terminal Parameter parameter;
non terminal List<VariableDeclaration> variableDeclarationList;
non terminal List<Statement> statementList;
non terminal Statement statement;
non terminal BlockStatement blockStatement;
non terminal IfStatement ifStatement;
non terminal WhileStatement whileStatement;
non terminal FunctionCallStatement functionCallStatement;
non terminal DoStatement doStatement;
non terminal AssignmentStatement assignmentStatement;
non terminal List<Expression> expressionList;
non terminal Expression expression;
non terminal Expression logicalOrExpression;
non terminal Expression logicalAndExpression;
non terminal Expression equalityExpression;
non terminal Expression relationalExpression;
non terminal Expression additiveExpression;
non terminal Expression multiplicativeExpression;
non terminal Expression unaryExpression;
non terminal Expression primaryExpression;
non terminal LocationExpression locationExpression;

non terminal ForStatement forStatement;
non terminal ContinueStatement continueStatement;
non terminal BreakStatement breakStatement;

precedence left ELSE;

start with program;

program ::= declarationList:dl {: RESULT = new Program(dl); :};

declarationList ::= declaration:d declarationList:dl {: dl.add(0, d); RESULT = dl; :}
           | {: RESULT = new ArrayList<Declaration>(); :};

declaration ::= variableDeclaration:vd {: RESULT = vd; :}
              | functionDeclaration:fd {: RESULT = fd; :};

functionDeclaration ::= VOID IDENTIFIER:i OPENPAREN parameter:p parameterList:pl CLOSEPAREN blockStatement:bs {: pl.add(0, p); RESULT = new FunctionDeclaration(i, pl, bs); :}
                      | VOID IDENTIFIER:i OPENPAREN CLOSEPAREN blockStatement:bs {: RESULT = new FunctionDeclaration(i, bs); :};

variableDeclaration ::= type:t variable:v variableList:vl SEMICOLON {: vl.add(0, v); RESULT = new VariableDeclaration(t, vl); :};

variableList ::= variableList:vl COMMA variable:v {: vl.add(0, v); RESULT = vl; :}
               | {: RESULT = new LinkedList<Variable>(); :} ;

variable ::= IDENTIFIER:i {: RESULT = new Variable(i); :}
           | IDENTIFIER:i OPENBRACKET INTLITERAL:il CLOSEBRACKET {: RESULT = new Variable(i, il); :};

type ::= BOOL:b {: RESULT = b; :}
       | INT:i {: RESULT = i; :};

parameterList ::= parameterList:pl COMMA parameter:p {: pl.add(0, p); RESULT = pl; :}
                | {: RESULT = new LinkedList<Parameter>(); :};

parameter ::= type:t IDENTIFIER:i OPENBRACKET CLOSEBRACKET {: RESULT = new Parameter(t, true, i, true); :}
            | type:t IDENTIFIER:i {: RESULT = new Parameter(t, false, i, false); :}
            | type:t AMPERSAND IDENTIFIER:i {: RESULT = new Parameter(t, true, i, false); :};

blockStatement ::= OPENBRACE variableDeclarationList:vdl statementList:sl CLOSEBRACE {: RESULT = new BlockStatement(vdl, sl); :};

variableDeclarationList ::= variableDeclaration:vd variableDeclarationList:vdl {: vdl.add(0, vd); RESULT = vdl; :}
                | {: RESULT = new ArrayList<VariableDeclaration>(); :};

statementList ::= statement:s statementList:sl {: sl.add(0, s); RESULT = sl; :}
                | {: RESULT = new ArrayList<Statement>(); :};

statement ::= blockStatement:bs {: RESULT = bs; :}
            | ifStatement:is {: RESULT = is; :}
            | whileStatement:ws {: RESULT = ws; :}
            | doStatement:ds {: RESULT = ds; :}
            | functionCallStatement:fcs {: RESULT = fcs; :}
            | assignmentStatement:as {: RESULT = as; :}
            | continueStatement:cs {: RESULT = cs; :}
            | breakStatement:bs {: RESULT = bs; :}
            | forStatement:fs {: RESULT = fs; :};

ifStatement ::= IF OPENPAREN expression:e CLOSEPAREN statement:s {: RESULT = new IfStatement(e, s, null); :}
              | IF OPENPAREN expression:e CLOSEPAREN statement:s ELSE statement:es {: RESULT = new IfStatement(e, s, es); :};

whileStatement ::= WHILE OPENPAREN expression:e CLOSEPAREN statement:s {: RESULT = new WhileStatement(e, s); :};

doStatement ::= DO statement:s WHILE OPENPAREN expression:e CLOSEPAREN SEMICOLON {: RESULT = new DoStatement(s, e); :};

functionCallStatement ::= IDENTIFIER:i OPENPAREN expression:e expressionList:el CLOSEPAREN SEMICOLON {: el.add(0, e); RESULT = new FunctionCallStatement(i, el); :}
                        | IDENTIFIER:i OPENPAREN CLOSEPAREN SEMICOLON {: RESULT = new FunctionCallStatement(i); :};

expressionList ::= expressionList:el COMMA expression:e {: el.add(0, e); RESULT = el; :}
                | {: RESULT = new ArrayList<Expression>(); :};


assignmentStatement ::= locationExpression:le EQUALS expression:e SEMICOLON {: RESULT = new AssignmentStatement(le, e); :};

expression ::= logicalOrExpression:e {: RESULT = e; :};

logicalOrExpression ::= logicalOrExpression:e1 BARBAR:o logicalAndExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                      | logicalAndExpression:e {: RESULT = e; :};

logicalAndExpression ::= logicalAndExpression:e1 AMPERSANDAMPERSAND:o equalityExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                       | equalityExpression:e {: RESULT = e; :};

equalityExpression ::= equalityExpression:e1 EQUALSEQUALS:o relationalExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                     | equalityExpression:e1 EXCLAMATIONEQUALS:o relationalExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                     | relationalExpression:e {: RESULT = e; :};

relationalExpression ::= relationalExpression:e1 LESSTHAN:o additiveExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                       | relationalExpression:e1 LESSTHANEQUALS:o additiveExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                       | relationalExpression:e1 GREATERTHAN:o additiveExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                       | relationalExpression:e1 GREATERTHANEQUALS:o additiveExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                       | additiveExpression:e {: RESULT = e; :};

additiveExpression ::= additiveExpression:e1 PLUS:o multiplicativeExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                     | additiveExpression:e1 MINUS:o multiplicativeExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                     | multiplicativeExpression:e {: RESULT = e; :};

multiplicativeExpression ::= multiplicativeExpression:e1 ASTERISK:o unaryExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                           | multiplicativeExpression:e1 SLASH:o unaryExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                           | multiplicativeExpression:e1 PERCENT:o unaryExpression:e2 {: RESULT = new BinaryExpression(e1, o, e2); :}
                           | unaryExpression:e {: RESULT = e; :};

unaryExpression ::= MINUS:o unaryExpression:e {: RESULT = new UnaryExpression(OperatorKind.Minus, e); :}
                  | EXCLAMATION:o unaryExpression:e {: RESULT = new UnaryExpression(o, e); :}
                  | primaryExpression:e {: RESULT = e; :};

primaryExpression ::= BOOLLITERAL:b {: RESULT = new BooleanExpression(b); :}
                    | INTLITERAL:i {: RESULT = new IntegerExpression(i); :}
                    | OPENPAREN expression:e CLOSEPAREN {: RESULT = e; :}
                    | locationExpression:le {: RESULT = le; :};

locationExpression ::= IDENTIFIER:i {: RESULT = new LocationExpression(i); :}
                     | IDENTIFIER:i OPENBRACKET expression:e CLOSEBRACKET {: RESULT = new LocationExpression(i, e); :};

forStatement ::= FOR OPENPAREN assignmentStatement:as1 expression:e1 SEMICOLON assignmentStatement:as2 CLOSEPAREN statement:s {: RESULT = new ForStatement(as1, e1, as2, s); :}
               | FOR OPENPAREN assignmentStatement:as1 expression:e1 SEMICOLON SEMICOLON CLOSEPAREN statement:s {: RESULT = new ForStatement(as1, e1, null, s); :}
               | FOR OPENPAREN assignmentStatement:as1 SEMICOLON assignmentStatement:as2 CLOSEPAREN statement:s {: RESULT = new ForStatement(as1, null, as2, s); :}
               | FOR OPENPAREN assignmentStatement:as1 SEMICOLON SEMICOLON CLOSEPAREN statement:s {: RESULT = new ForStatement(as1, null, null, s); :}
               | FOR OPENPAREN SEMICOLON expression:e1 SEMICOLON assignmentStatement:as2 CLOSEPAREN statement:s {: RESULT = new ForStatement(null, e1, as2, s); :}
               | FOR OPENPAREN SEMICOLON expression:e1 SEMICOLON SEMICOLON CLOSEPAREN statement:s {: RESULT = new ForStatement(null, e1, null, s); :}
               | FOR OPENPAREN SEMICOLON SEMICOLON assignmentStatement:as2 CLOSEPAREN statement:s {: RESULT = new ForStatement(null, null, as2, s); :}
               | FOR OPENPAREN SEMICOLON SEMICOLON SEMICOLON CLOSEPAREN statement:s {: RESULT = new ForStatement(null, null, null, s); :};

breakStatement ::= BREAK SEMICOLON {: RESULT = new BreakStatement(); :};

continueStatement ::= CONTINUE SEMICOLON {: RESULT = new ContinueStatement(); :};
